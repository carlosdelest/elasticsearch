knnSearch
required_capability: knn_function

// tag::knn-function[]
from colors metadata _score 
| where knn(rgb_vector, [0, 120, 0]) 
| sort _score desc 
// end::knn-function[]
| keep color, rgb_vector
;

// tag::knn-function-result[]
color:text       | rgb_vector:dense_vector
green            | [0.0, 128.0, 0.0]      
dark green       | [0.0, 100.0, 0.0]      
forest green     | [34.0, 139.0, 34.0]    
dark olive green | [85.0, 107.0, 47.0]    
sea green        | [46.0, 139.0, 87.0]    
dark slate gray  | [47.0, 79.0, 79.0]     
olive drab       | [107.0, 142.0, 35.0]   
lime green       | [50.0, 205.0, 50.0]    
black            | [0.0, 0.0, 0.0]        
olive            | [128.0, 128.0, 0.0]    
// end::knn-function-result[]
;

knnSearchWithKOption
required_capability: knn_function

// tag::knn-function-options[]
from colors metadata _score 
| where knn(rgb_vector, [0,255,255], {"k": 4})
| sort _score desc 
// end::knn-function-options[]
| keep color, rgb_vector
;

color:text     | rgb_vector:dense_vector
cyan           | [0.0, 255.0, 255.0]    
deep sky blue  | [0.0, 191.0, 255.0]    
dark turquoise | [0.0, 206.0, 209.0]    
turquoise      | [64.0, 224.0, 208.0]   
;

knnSearchWithSimilarityOption
required_capability: knn_function

from colors metadata _score 
| where knn(rgb_vector, [255,192,203], {"similarity": 40})
| sort _score desc 
| keep color, rgb_vector
;

color:text       | rgb_vector:dense_vector
pink             | [255.0, 192.0, 203.0]
light pink       | [255.0, 182.0, 193.0]
peach puff       | [255.0, 218.0, 185.0]
bisque           | [255.0, 228.0, 196.0]
thistle          | [216.0, 191.0, 216.0]
wheat            | [245.0, 222.0, 179.0]
;

knnHybridSearch
required_capability: knn_function

from colors metadata _score 
| where match(color, "violet") or knn(rgb_vector, [238,130,238], {"boost": 10.0, "k": 5})
| sort _score desc 
| eval round_score = round(_score, 4)
| keep color, rgb_vector, round_score
;

color:text        | rgb_vector:dense_vector | round_score:double
violet            | [238.0, 130.0, 238.0]   | 13.9457           
blue violet       | [138.0, 43.0, 226.0]    | 3.0871            
dark violet       | [148.0, 0.0, 211.0]     | 3.0871            
medium violet red | [199.0, 21.0, 133.0]    | 2.5355            
pale violet red   | [219.0, 112.0, 147.0]   | 2.5355            
orchid            | [218.0, 112.0, 214.0]   | 0.0083            
plum              | [221.0, 160.0, 221.0]   | 0.0071            
hot pink          | [255.0, 105.0, 180.0]   | 0.0024            
thistle           | [216.0, 191.0, 216.0]   | 0.0021  
;

knnWithMultipleFunctions
required_capability: knn_function

from colors metadata _score
| where knn(rgb_vector, [128,128,0]) and match(color, "olive") 
| sort _score desc
| eval round_score = round(_score, 4)
| keep color, rgb_vector, round_score
;

color:text       | rgb_vector:dense_vector | round_score:double
olive            | [128.0, 128.0, 0.0]     | 5.4979            
olive drab       | [107.0, 142.0, 35.0]    | 3.5206            
dark olive green | [85.0, 107.0, 47.0]     | 2.8906 
;

knnAfterKeep
required_capability: knn_function

from colors metadata _score
| keep rgb_vector, color, _score 
| where knn(rgb_vector, [128,128,0]) 
| eval round_score = round(_score, 4)
| sort round_score desc, color asc
| keep rgb_vector, round_score
| limit 5
;

rgb_vector:dense_vector | round_score:double
[128.0, 128.0, 0.0]     | 1.0               
[107.0, 142.0, 35.0]    | 0.0014            
[85.0, 107.0, 47.0]     | 4.0E-4            
[184.0, 134.0, 11.0]    | 3.0E-4            
[139.0, 69.0, 19.0]     | 3.0E-4   
;

knnAfterDrop
required_capability: knn_function

from colors metadata _score
| drop color
| where knn(rgb_vector, [128,128,0]) 
| eval round_score = round(_score, 4)
| keep rgb_vector, round_score
| limit 5
;

rgb_vector:dense_vector | round_score:double
[184.0, 134.0, 11.0]    | 3.0E-4            
[128.0, 128.0, 0.0]     | 1.0               
[154.0, 205.0, 50.0]    | 1.0E-4            
[85.0, 107.0, 47.0]     | 4.0E-4            
[107.0, 142.0, 35.0]    | 0.0014   
;

knnAfterEval
required_capability: knn_function

from colors metadata _score
| eval composed_name = locate(color, " ") > 0 
| where knn(rgb_vector, [128,128,0]) 
| sort _score, color desc
| keep color, composed_name 
;

color:text       | composed_name:boolean
peru             | false                
yellow green     | true                 
chocolate        | false                
dim gray         | true                 
saddle brown     | true                 
sienna           | false                
dark golden rod  | true                 
dark olive green | true                 
olive drab       | true                 
olive            | false 
;

knnWithConjunction
required_capability: knn_function

# TODO We need kNN prefiltering here so we get more candidates that pass the filter
from colors metadata _score 
| where knn(rgb_vector, [255,255,238]) and hex_code like "#FFF*" 
| keep color, hex_code, rgb_vector
;
ignoreOrder:true

color:text        | hex_code: keyword    | rgb_vector:dense_vector
light yellow      | #FFFFE0              | [255.0, 255.0, 224.0]
lavender blush    | #FFF0F5              | [255.0, 240.0, 245.0]
sea shell         | #FFF5EE              | [255.0, 245.0, 238.0]
floral white      | #FFFAF0              | [255.0, 250.0, 240.0]
ivory             | #FFFFF0              | [255.0, 255.0, 240.0]
snow              | #FFFAFA              | [255.0, 250.0, 250.0]
white             | #FFFFFF              | [255.0, 255.0, 255.0]
;

knnWithDisjunctionAndFiltersConjunction
required_capability: knn_function

# TODO We need kNN prefiltering here so we get more candidates that pass the filter
from colors metadata _score 
| where (knn(rgb_vector, [0,255,255]) or knn(rgb_vector, [128, 0, 255])) and primary == true 
| keep color, rgb_vector, _score
;

color:text        | rgb_vector:dense_vector | _score:double
cyan              | [0.0, 255.0, 255.0]     | 1.0                 
blue              | [0.0, 0.0, 255.0]       | 9.922293975250795E-5
;

knnWithDisjunctionAndConjunction
required_capability: knn_function
required_capability: full_text_functions_disjunctions

# TODO We need kNN prefiltering here so we get more candidates that pass the filter
from colors metadata _score 
| where (knn(rgb_vector, [0,255,255]) or knn(rgb_vector, [0, 0, 255])) and knn(rgb_vector, [0, 255, 0])
| keep color, rgb_vector, _score
;

color:text          | rgb_vector:dense_vector | _score:double
medium spring green | [0.0, 250.0, 154.0]     | 1.6871128173079342E-4
;

knnWithNonPushableConjunction
required_capability: knn_function

from colors metadata _score
| eval composed_name = locate(color, " ") > 0 
| where knn(rgb_vector, [128,128,0]) and composed_name == false
| eval round_score = round(_score, 4)
| keep color, composed_name, round_score
;

color:text | composed_name:boolean | round_score:double
olive      | false                 | 1.0               
sienna     | false                 | 3.0E-4            
chocolate  | false                 | 1.0E-4            
peru       | false                 | 1.0E-4    
;

testKnnWithNonPushableDisjunctions
required_capability: knn_function

from colors metadata _score 
| where knn(rgb_vector, [128,128,0], {"k": 5}) or length(color) > 17 
| sort _score desc, color asc
| eval round_score = round(_score, 4)
| keep color, round_score 
;

color:text              | round_score:double
olive                   | 1.0
olive drab              | 0.0014
dark olive green        | 4.0E-4
dark golden rod         | 3.0E-4
sienna                  | 3.0E-4
light golden rod yellow | 0.0
medium aqua marine      | 0.0
medium spring green     | 0.0
;

testKnnWithNonPushableDisjunctionsOnComplexExpressions
required_capability: knn_function

from colors metadata _score 
| where (knn(rgb_vector, [128,128,0]) and length(color) > 12) or (knn(rgb_vector, [128,0,128]) and primary == false) 
| sort _score desc 
| eval round_score = round(_score, 4) 
| keep color, primary, round_score
;

color: text       | primary: boolean   | round_score: double  
purple            | false              | 1.0            
dark magenta      | false              | 0.0045         
dark olive green  | false              | 4.0E-4         
indigo            | false              | 4.0E-4         
dark golden rod   | false              | 3.0E-4         
dim gray          | false              | 3.0E-4         
dark slate blue   | false              | 2.0E-4         
medium violet red | false              | 2.0E-4         
dark orchid       | false              | 1.0E-4         
dark violet       | false              | 1.0E-4         
brown             | false              | 1.0E-4         
blue violet       | false              | 1.0E-4         
;

testKnnInStatsNonPushable
required_capability: knn_function

from colors 
| where length(color) < 10 
| stats c = count(*) where knn(rgb_vector, [128,128,255], {"k": 140})
;

c: long 
59      
;

testKnnInStatsPushableAndNonPushable
required_capability: knn_function
required_capability: full_text_functions_in_stats_where

from colors metadata _score 
| stats c = count(*) where (knn(rgb_vector, [0,255,255], {"k": 140}) or knn(rgb_vector, [0, 0, 255])) and knn(rgb_vector, [0, 255, 0], {"k": 40})
;

c:long
40
;

testKnnInStatsWithGrouping
required_capability: knn_function
required_capability: full_text_functions_in_stats_where

from colors 
| where length(color) < 10 
| stats c = count(*) where knn(rgb_vector, [128,128,255], {"k": 140}) by primary
;

c: long       | primary: boolean    
50            | false          
9             | true           
;

testKnnInStatsPushable
required_capability: knn_function
required_capability: full_text_functions_in_stats_where

from colors
| stats c = count(*) where knn(rgb_vector, [128,128,255], {"k": 40})
;

# No surprises, gets the number of top k
c:long
40
;
